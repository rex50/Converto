plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.rex50.converto'
    compileSdk 35

    defaultConfig {
        applicationId "com.rex50.converto"
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName "1.0"

        def apiKey = ""
        def localPropertiesFile = project.rootProject.file('local.properties')

        // Check if running in CI/CD (GitHub Actions sets CI env variable)
        if (System.getenv("OPEN_EXCHANGE_SERVICE_KEY")) {
            apiKey = System.getenv("OPEN_EXCHANGE_SERVICE_KEY")
        } else if (localPropertiesFile.exists()) {
            // Read from local.properties for local development
            Properties localProperties = new Properties()
            localProperties.load(localPropertiesFile.newDataInputStream())
            apiKey = localProperties.getProperty('OPEN_EXCHANGE_API_KEY', '')
        }

        buildConfigField "String", "OPEN_EXCHANGE_SERVICE_URL", "\"https://openexchangerates.org/api/\""
        buildConfigField "String", "OPEN_EXCHANGE_SERVICE_KEY", "\"${apiKey}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.activity:activity-compose:1.9.3'

    // Compose BOM for version management
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0"
    implementation "com.holix.android:bottomsheetdialog-compose:1.3.0"

    // Kotlin Coroutines - Threading
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"

    // Retrofit - Networking
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.12.0"

    // Hilt - Dependency Injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"

    // Hilt for Compose
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"

    // Work manager (Kotlin + coroutines)
    implementation "androidx.work:work-runtime-ktx:$worker_version"

    // Hilt with Work Manager
    implementation "androidx.hilt:hilt-work:$hilt_worker_version"
    ksp "androidx.hilt:hilt-compiler:$hilt_worker_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.8.5"

    // Joda - DateTime Utils
    implementation "net.danlew:android.joda:$joda_version"

    // mockk - For unit testing
    testImplementation "io.mockk:mockk-android:${mockkVersion}"
    testImplementation "io.mockk:mockk-agent:${mockkVersion}"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20240303'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_android_version"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_bom_version")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}